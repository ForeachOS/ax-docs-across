buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16")
		classpath("org.asciidoctor:asciidoctorj-epub3:1.5.0-alpha.7")
	}
}

plugins {
	id "org.asciidoctor.convert" version "1.5.6"
}

group = 'com.foreach.across'
version = '3.0.0-SNAPSHOT'

repositories {
	mavenCentral()
}


asciidoctor {
	sources {
		include '*.adoc'
	}
	sourceDir = file("${rootProject.projectDir}/docs")
	resources {
		from(file("${rootProject.projectDir}/ax-docs-shared")) {
			include 'stylesheets/*'
		}
	}
	logDocuments = true
	backends = ["html5"]

	options doctype: 'book', eruby: 'erubis'
	attributes  'icons': 'font',
			'idprefix': '',
			'idseparator': '-',
			docinfo: '',
			linkcss: '',
			revnumber: project.version,
			sectanchors: '',
			sectnums: '',
			'source-highlighter': 'coderay@', // TODO switch to 'rouge' once supported by the html5 backend
			stylesdir: 'stylesheets/',
			stylesheet: 'across.css',
			'spring-version': project.version

}

task docsZip(type: Zip, dependsOn: ['asciidoctor']) {
	group = "Distribution"
	baseName = "across"
	classifier = "docs"
	description = "Builds -${classifier} archive containing reference " +
			"for deployment at http://docs-across.foreach.be"

	from ("${buildDir}/asciidoc/html5") {
		into "reference"
	}
}
